DOCKER_USER := "spilikin"
ZERO_VERSION := "0.13.1"
ZERO_EPA_VERSION := ZERO_VERSION
ZERO_PDP_VERSION := ZERO_VERSION
ZERO_CADDY_VERSION := ZERO_VERSION
TI_CLI_VERSION := ZERO_VERSION

# Display available commands
info:
    just --list


# Build the zero-epa docker image
docker-build-epa: generate-versions
    docker build --platform linux/amd64 -t "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}" -f ./epa/cmd/zero-epa/Dockerfile .
    docker tag "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}" "{{DOCKER_USER}}/zero-epa:latest"

# Push the zero-epa docker image to the docker hub
docker-push-epa: docker-build-epa
    docker push "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}"
    docker push "{{DOCKER_USER}}/zero-epa:latest"    

# Build the zero-caddy docker image
docker-build-caddy: generate-versions
    docker build --platform linux/amd64 -t "{{DOCKER_USER}}/zero-caddy:{{ZERO_CADDY_VERSION}}" -f ./zaddy/cmd/zero-caddy/Dockerfile .
    docker tag "{{DOCKER_USER}}/zero-caddy:{{ZERO_CADDY_VERSION}}" "{{DOCKER_USER}}/zero-caddy:latest"

# Push the zero-caddy docker image to the docker hub
docker-push-caddy: docker-build-caddy
    docker push "{{DOCKER_USER}}/zero-caddy:{{ZERO_CADDY_VERSION}}"
    docker push "{{DOCKER_USER}}/zero-caddy:latest"    

# Build the zero-pdp docker image
docker-build-pdp: generate-versions
    docker build --platform linux/amd64 -t "{{DOCKER_USER}}/zero-pdp:{{ZERO_PDP_VERSION}}" -f ./pdp/cmd/zero-pdp/Dockerfile .
    docker tag "{{DOCKER_USER}}/zero-pdp:{{ZERO_PDP_VERSION}}" "{{DOCKER_USER}}/zero-pdp:latest"

# Push the zero-pdp docker image to the docker hub
docker-push-pdp: docker-build-pdp
    docker push "{{DOCKER_USER}}/zero-pdp:{{ZERO_PDP_VERSION}}"
    docker push "{{DOCKER_USER}}/zero-pdp:latest"

# generate version file
generate-version-file file package version:
    @echo "package {{package}}" > {{file}}
    @echo "" >> {{file}}
    @echo "const Version = \"{{version}}\"" >> {{file}}

# generate version files for all packages
generate-versions:
    just generate-version-file epa/version.go epa {{ZERO_EPA_VERSION}}
    just generate-version-file pdp/version.go pdp {{ZERO_PDP_VERSION}}
    just generate-version-file ti/version.go main {{TI_CLI_VERSION}}

