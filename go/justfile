DOCKER_USER := "spilikin"
ZERO_VERSION := "0.10.1"
ZERO_EPA_VERSION := ZERO_VERSION
ZERO_PDP_VERSION := ZERO_VERSION

# Display available commands
info:
    just --list


# Build the zero-epa docker image
epa-docker-build:
    docker build --platform linux/amd64 -t "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}" -f ./epa/cmd/zero-epa/Dockerfile .
    docker tag "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}" "{{DOCKER_USER}}/zero-epa:latest"

# Push the zero-epa docker image to the docker hub
epa-docker-push: epa-docker-build
    docker push "{{DOCKER_USER}}/zero-epa:{{ZERO_EPA_VERSION}}"
    docker push "{{DOCKER_USER}}/zero-epa:latest"    

# generate version file
generate-version-file file package version:
    @echo "package {{package}}" > {{file}}
    @echo "" >> {{file}}
    @echo "const Version = \"{{version}}\"" >> {{file}}

# generate version files for all packages
generate-versions:
    just generate-version-file epa/version.go epa {{ZERO_EPA_VERSION}}
    just generate-version-file pdp/version.go pdp {{ZERO_PDP_VERSION}}
